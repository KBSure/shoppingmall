buildscript {
	ext {
		springBootVersion = '2.0.1.RELEASE'
		querydslVersion    = "4.1.4"
		metaModelsSourcesDir = file("target/querydsl/java/")
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id 'io.franzbecker.gradle-lombok' version '1.12'
	id "com.ewerk.gradle.plugins.querydsl" version "1.0.9"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

gradle.projectsLoaded {
	rootProject.allprojects {
		buildDir = "build"
	}
}

idea {
    module {
        sourceDirs += file('target/querydsl/java/')
        generatedSourceDirs += file('target/querydsl/java/')
        outputDir file('build/classes/main')
        testOutputDir file('build/classes/test')
    }
}

sourceSets {
	main {
		java {
			srcDir metaModelsSourcesDir
		}
	}
}

repositories {
	mavenCentral()
}

group = 'examples.boot'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

project.afterEvaluate {
	project.tasks.compileQuerydsl.doFirst {
		delete metaModelsSourcesDir;
		metaModelsSourcesDir.mkdirs();
	}
	project.tasks.compileQuerydsl.options.compilerArgs = [
			"-proc:only",
			"-processor", 'com.querydsl.apt.jpa.JPAAnnotationProcessor' +
					',lombok.launch.AnnotationProcessorHider$AnnotationProcessor',
			"-s",metaModelsSourcesDir
	]
}

clean{
	delete metaModelsSourcesDir;
}

querydsl {
	querydslSourcesDir = "target/querydsl/java"
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-thymeleaf')
	compile group: 'nz.net.ultraq.thymeleaf', name: 'thymeleaf-layout-dialect', version: '2.3.0'
	compile("com.fasterxml.jackson.datatype:jackson-datatype-hibernate5")
	compile('org.springframework.boot:spring-boot-starter-web')
	runtime('org.springframework.boot:spring-boot-devtools')
	compile('org.projectlombok:lombok')
	runtime('mysql:mysql-connector-java')
	compile("com.querydsl:querydsl-sql-spatial:${querydslVersion}")
	compile("com.querydsl:querydsl-jpa:${querydslVersion}")
	compile group: 'org.webjars', name: 'webjars-locator', version: '0.34'
	compile 'org.webjars:bootstrap:4.1.0'
	compile 'org.webjars:jquery:3.3.1-1'
	compile group: 'com.h2database', name: 'h2', version: '1.4.197'
	testCompile('org.springframework.boot:spring-boot-starter-test')
	//testCompile('org.springframework.security:spring-security-test')
}